#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : SendingEmail
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x141da591056f99d2
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     code : |1-
      
      ST_SMTP_CONFIGURATION is Structure
      	sServerAddress		is string
      	sUserLogin	is string
      	sUserPWD		is string
      	nPort				is int
      	bSecuredMode		is boolean	
      end
      
      gstConfig_SMTP is ST_SMTP_CONFIGURATION
      
      constant
      
      	// Type of the site parameters
      	CN_INTEGER = 1
      	CN_STRING = 2
      	CN_REAL = 4
      	CN_BINARY = 5
      		
      	// Constants of the history
      	CN_HISTORY_WEB_TYPE			= 0
      	
      	// Email configuration
      	csAdmin_Contact_EmailSender		= "CONTACT_EMAIL_SENDER"		// Email address for sending the contact form
      	csAdmin_Contact_RecipientEmail	= "CONTACT_EMAIL_RECIPIENT"	// Recipient email address for the contact form
      	csAdmin_SMTPCONFIG					= "CONFIG_SMTP"					// Configuration of the SMTP server for sending emails
      end
     type : 720896
  procedures :
   -
     name : ReadSMTPConfiguration
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 1449496697363274194
     type_code : 15
     code : |1+
      
      
      // Summary: Returns the configuration of the SMTP server 
      // Syntax:
      //[ <Result> = ] ReadSMTPConfiguration ()
      //
      // Parameters:
      //	None
      // Return value:
      // 	ST_SMTP_CONFIGURATION: SMTP configuration 	
      //// Automatic process of exceptions:   run CASE EXCEPTION:
      //
      
      procedure ReadSMTPConfiguration()
      
      // Local variables
      stConfiguration 			is ST_SMTP_CONFIGURATION
      sSerializedConfiguration	is string
      
      // Default configuration
      stConfiguration.nPort = 25
      stConfiguration.sServerAddress = "localhost"
      
      // Initialize the mechanism for error feedback
      InitializeError()
      
      // Retrieve the stored content
      sSerializedConfiguration = GetSiteParameter(csAdmin_SMTPCONFIG, "")
      
      // Deserialization
      if sSerializedConfiguration <> "" then
      	when exception in
      		Deserialize(stConfiguration,sSerializedConfiguration,psdXML)
      	do
      		AddError("A format problem has been detected",ErrorInfo())
      	end
      else
      	
      end
      
      // Return the configuration
      return stConfiguration
      
      case exception:
      AddError("Exception detected",ExceptionInfo(errFullDetails))
      return stConfiguration
      
      
      
     type : 458752
   -
     name : SendContactMessage
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 1449496697363339730
     type_code : 15
     code : |1+
      // Summary: Sends a pre-booking message
      // Syntax:
      //[ <Result> = ] SendContactMessage (<sCustomerEmail> is string, <sHTMLMessage> is string)
      //
      // Parameters:
      //	sClientEmail (ANSI string): Email of the client
      //	sHTMLMessage (ANSI string): Text of message
      // Return value:
      // 	boolean: True if the message was successfully sent, False otherwise
      //
      //// Automatic process of exceptions:   run CASE EXCEPTION:
      //
      
      procedure SendContactMessage(sCustomerEmail is string, sHTMLMessage is string)
      
      // Local variables
      sRecipient		is string
      sSender			is string
      
      // Read the configuration
      gstConfig_SMTP 		= ReadSMTPConfiguration()
      sRecipient		= ReadEmailAddressFormRecipient()
      sSender			= sCustomerEmail
      
      // Reinitialize the variables 
      EmailReset()
      
      // Start the SMTP session
      if StartSMTPSession(gstConfig_SMTP) = False then return False
      
      // Fill the structure
      Email.NbRecipient 	= 1
      Email.Sender 		= sSender
      Email.Recipient[1]	= sRecipient
      Email.Subject 			= "Pre-registration form from the website"
      Email.HTML				= sHTMLMessage
      
      // Send the message
      if EmailSendMessage(gstConfig_SMTP.sUserLogin) = False then 
      	AddError("The message was not sent",ErrorInfo())
      	return False
      end
      
      // Close the POP3 session
      EmailCloseSession(gstConfig_SMTP.sUserLogin)	
      
      // The sending is successfully performed by default
      return True
      
      case exception:
      AddError("Exception detected",ExceptionInfo())
      
      // Close the SMTP session
      EmailCloseSession(gstConfig_SMTP.sUserLogin)	
      
      // Message not sent
      return False
     type : 458752
   -
     name : StartSMTPSession
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 1449496697363405266
     type_code : 15
     code : |1+
      // Summary: Starts an SMTP session
      // Syntax:
      //[ <Result> = ] StartSMTPSession (<stConfiguration> is ST_SMTP_CONFIGURATION)
      //
      // Parameters:
      //	stConfiguration (ST_SMTP_CONFIGURATION): <specify the role of stConfiguration>
      // Return value:
      // 	boolean: True if the session was started, False otherwise
      //
      //// Automatic process of exceptions:   run CASE EXCEPTION:
      //
      
      procedure StartSMTPSession(stConfiguration is ST_SMTP_CONFIGURATION)
      
      // Configure the maximum time-out
      EmailSetTimeOut(10)
      
      // Start the SMTP session
      if stConfiguration.bSecuredMode = True then
      	if EmailStartSMTPSession(stConfiguration.sUserLogin,stConfiguration.sUserPWD,stConfiguration.sServerAddress,stConfiguration.nPort,emailOptionSecuredTLS) = False then
      		AddError("Unable to start the SMTP session",ErrorInfo(),False)
      		return False
      	end
      else
      	if EmailStartSMTPSession(stConfiguration.sUserLogin,stConfiguration.sUserPWD,stConfiguration.sServerAddress,stConfiguration.nPort,emailOptionDefault) = False then
      		AddError("Unable to start the SMTP session",ErrorInfo(),False)	
      		return False
      	end	
      end
      
      // The connection is established by default
      return True
      
      case exception:
      AddError("Exception detected",ExceptionInfo())
      return False
      
     type : 458752
   -
     name : ReadEmailAddressFormRecipient
     procedure_id : 1449496697363470802
     type_code : 15
     code : |1-
      // Summary: Returns the recipient email address for the contacts
      // Syntax:
      //[ <Result> = ] ReadEmailAddressFormRecipient ()
      //
      // Parameters:
      //	None
      // Return value:
      // 	Undefined type: Email address
      //
      
      procedure ReadEmailAddressFormRecipient()
      
      // Initialize the mechanism for error feedback
      InitializeError()
      
      return  GetSiteParameter(csAdmin_Contact_RecipientEmail,"")
     type : 458752
   -
     name : ModifyEmailAddressRecipientCompanyContact
     procedure_id : 1449496697363536338
     type_code : 15
     code : |1-
      // Summary: Modifies the email address used for taking contact (send a copy)
      // Syntax:
      //[ <Result> = ] ModifyEmailAddressSenderCompanyContact (<sEmailAddress> is string)
      //
      // Parameters:
      //	sEmailAddress (ANSI string): New email address
      // Return value:
      // 	boolean: True if the modification was performed, False otherwise
      //
      
      procedure ModifyEmailAddressRecipientCompanyContact(sEmailAddress is string)
      
      // Initialize the mechanism for error feedback
      InitializeError()
      
      return SaveSiteParameter(csAdmin_Contact_RecipientEmail,sEmailAddress,CN_STRING)
     type : 458752
   -
     name : ModifySMTPConfiguration
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 1449496697363601874
     type_code : 15
     code : |1-
      // Summary: Modifies the configuration of the SMTP server 
      // Syntax:
      //[ <Result> = ] ModifySMTPConfiguration (<stNewConfiguration> is ST_SMTP_CONFIGURATION)
      //
      // Parameters:
      //	stNewConfiguration (ST_SMTP_CONFIGURATION): New configuration of the SMTP server
      // Return value:
      // 	boolean: True if the modification was performed, False otherwise
      //
      //// Automatic process of exceptions:   run CASE EXCEPTION:
      //
      
      procedure ModifySMTPConfiguration(stNewConfiguration is ST_SMTP_CONFIGURATION)
      
      // Local variables
      sSerializedConfiguration	is string
      
      // Initialize the mechanism for error feedback
      InitializeError()
      
      // Serialization
      when exception in
      	Serialize(stNewConfiguration,sSerializedConfiguration,psdXML)
      do
      	AddError("A format problem has been detected",ErrorInfo())
      	return False
      end
      
      // Save in database
      if SaveSiteParameter(csAdmin_SMTPCONFIG, sSerializedConfiguration, CN_STRING) = False then	
      	return False
      end
      
      // Modification successfully performed
      return True
      
      case exception:
      	AddError("Exception detected",ExceptionInfo(errFullDetails))
      	return False
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x14748da90ed88d18
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      en-GB : User not identified or without sufficient rights
     index : 0
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
